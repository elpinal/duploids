module Duploid where

import Relation.Binary.PropositionalEquality as Eq
open Eq using (_â‰¡_; refl; sym; cong)
open Eq.â‰¡-Reasoning

open import Level

open import Preduploid

private
  variable p q r s : Polarity

record Duploid o â„“ : Set (suc (o âŠ” â„“)) where
  field
    ğ’Ÿ : Preduploid o â„“

  open Preduploid.Preduploid ğ’Ÿ public

  field
    â‡“ : Ob âŠ -> Ob +
    â‡‘ : Ob + -> Ob âŠ

    delay : forall {P : Ob +} -> P â‡’ â‡‘ P
    force : forall {P : Ob +} -> â‡‘ P â‡’ P

    wrap : forall {N : Ob âŠ} -> N â‡’ â‡“ N
    unwrap : forall {N : Ob âŠ} -> â‡“ N â‡’ N

    forceâˆ˜delayâˆ™ : forall {A : Ob p} {P : Ob +} {f : A â‡’ P} -> force âˆ˜ (delay âˆ™ f) â‰¡ f
    âˆ˜unwrapâˆ™wrap : forall {N : Ob âŠ} {A : Ob p} {f : N â‡’ A} -> (f âˆ˜ unwrap) âˆ™ wrap â‰¡ f
    delayâˆ™force : forall {P : Ob +} -> delay âˆ™ force â‰¡ id {A = â‡‘ P}
    wrapâˆ˜unwrap : forall {N : Ob âŠ} -> wrap âˆ˜ unwrap â‰¡ id {A = â‡“ N}

  âˆ™forceâˆ˜delay : forall {P : Ob +} {A : Ob p} {g : P â‡’ A} -> (g âˆ™ force) âˆ˜ delay â‰¡ g
  âˆ™forceâˆ˜delay {g = g} =
    begin
      (g âˆ™ force) âˆ˜ delay
    â‰¡âŸ¨ assocâˆ™âˆ˜ âŸ©
      g âˆ™ (force âˆ˜ delay)
    â‰¡Ë˜âŸ¨ cong (g âˆ™_) (cong (force âˆ˜_) identityâˆ™Ê³) âŸ©
      g âˆ™ (force âˆ˜ (delay âˆ™ id))
    â‰¡âŸ¨ cong (g âˆ™_) forceâˆ˜delayâˆ™ âŸ©
      g âˆ™ id
    â‰¡âŸ¨ identityâˆ™Ê³ âŸ©
     g
    âˆ

  unwrapâˆ™wrapâˆ˜ : forall {A : Ob p} {N : Ob âŠ} {g : A â‡’ N} -> unwrap âˆ™ (wrap âˆ˜ g) â‰¡ g
  unwrapâˆ™wrapâˆ˜ {g = g} =
    begin
      unwrap âˆ™ (wrap âˆ˜ g)
    â‰¡Ë˜âŸ¨ assocâˆ™âˆ˜ âŸ©
      (unwrap âˆ™ wrap) âˆ˜ g
    â‰¡Ë˜âŸ¨ cong (_âˆ˜ g) (cong (_âˆ™ wrap) identityâˆ˜Ë¡) âŸ©
      ((id âˆ˜ unwrap) âˆ™ wrap) âˆ˜ g
    â‰¡âŸ¨ cong (_âˆ˜ g) âˆ˜unwrapâˆ™wrap âŸ©
      id âˆ˜ g
    â‰¡âŸ¨ identityâˆ˜Ë¡ âŸ©
      g
    âˆ

  wrap-Thunkable : forall {N : Ob âŠ} -> Thunkable (wrap {N = N})
  wrap-Thunkable {r = +} = sym assocâˆ™âˆ™
  wrap-Thunkable {r = âŠ} {g = g} {h = h} =
    begin
      h âˆ˜ (g âˆ™ wrap)
    â‰¡Ë˜âŸ¨ âˆ˜unwrapâˆ™wrap âŸ©
      ((h âˆ˜ (g âˆ™ wrap)) âˆ˜ unwrap) âˆ™ wrap
    â‰¡âŸ¨ cong (_âˆ™ wrap) assocâˆ˜âˆ˜ âŸ©
      (h âˆ˜ ((g âˆ™ wrap) âˆ˜ unwrap)) âˆ™ wrap
    â‰¡âŸ¨ cong (_âˆ™ wrap) (cong (h âˆ˜_) assocâˆ™âˆ˜) âŸ©
      (h âˆ˜ (g âˆ™ (wrap âˆ˜ unwrap))) âˆ™ wrap
    â‰¡âŸ¨ cong (_âˆ™ wrap) (cong (h âˆ˜_) (cong (g âˆ™_) wrapâˆ˜unwrap)) âŸ©
      (h âˆ˜ (g âˆ™ id)) âˆ™ wrap
    â‰¡âŸ¨ cong (_âˆ™ wrap) (cong (h âˆ˜_) identityâˆ™Ê³) âŸ©
      (h âˆ˜ g) âˆ™ wrap
    âˆ

  force-Linear : forall {P : Ob +} -> Linear (force {P = P})
  force-Linear {q = +} {g = g} {h = h} = sym (
    begin
      (force âˆ˜ g) âˆ™ h
    â‰¡Ë˜âŸ¨ forceâˆ˜delayâˆ™ âŸ©
      force âˆ˜ (delay âˆ™ ((force âˆ˜ g) âˆ™ h))
    â‰¡Ë˜âŸ¨ cong (force âˆ˜_) assocâˆ™âˆ™ âŸ©
      force âˆ˜ ((delay âˆ™ (force âˆ˜ g)) âˆ™ h)
    â‰¡Ë˜âŸ¨ cong (force âˆ˜_) (cong (_âˆ™ h) assocâˆ™âˆ˜) âŸ©
      force âˆ˜ (((delay âˆ™ force) âˆ˜ g) âˆ™ h)
    â‰¡âŸ¨ cong (force âˆ˜_) (cong (_âˆ™ h) (cong (_âˆ˜ g) delayâˆ™force)) âŸ©
      force âˆ˜ ((id âˆ˜ g) âˆ™ h)
    â‰¡âŸ¨ cong (force âˆ˜_) (cong (_âˆ™ h) identityâˆ˜Ë¡) âŸ©
      force âˆ˜ (g âˆ™ h)
    âˆ)
  force-Linear {q = âŠ} = sym assocâˆ˜âˆ˜

  helper1 : forall {A : Ob p} {P : Ob +} {f : A â‡’ P}
    -> (wrap âˆ˜ delay) âˆ™ f â‰¡ wrap âˆ˜ (delay âˆ™ f)
    -> forall {B : Ob q} {h : â‡‘ P â‡’ B} -> (h âˆ˜ delay) âˆ™ f â‰¡ h âˆ˜ (delay âˆ™ f)
  helper1 {f = f} e {h = h} =
    begin
      (h âˆ˜ delay) âˆ™ f
    â‰¡Ë˜âŸ¨ cong (_âˆ™ f) (cong (_âˆ˜ delay) âˆ˜unwrapâˆ™wrap) âŸ©
      (((h âˆ˜ unwrap) âˆ™ wrap) âˆ˜ delay) âˆ™ f
    â‰¡âŸ¨ cong (_âˆ™ f) assocâˆ™âˆ˜ âŸ©
      ((h âˆ˜ unwrap) âˆ™ (wrap âˆ˜ delay)) âˆ™ f
    â‰¡âŸ¨ assocâˆ™âˆ™ âŸ©
      (h âˆ˜ unwrap) âˆ™ ((wrap âˆ˜ delay) âˆ™ f)
    â‰¡âŸ¨ cong ((h âˆ˜ unwrap) âˆ™_) e âŸ©
      (h âˆ˜ unwrap) âˆ™ (wrap âˆ˜ (delay âˆ™ f))
    â‰¡Ë˜âŸ¨ assocâˆ™âˆ˜ âŸ©
      ((h âˆ˜ unwrap) âˆ™ wrap) âˆ˜ (delay âˆ™ f)
    â‰¡âŸ¨ cong (_âˆ˜ _) âˆ˜unwrapâˆ™wrap âŸ©
      h âˆ˜ (delay âˆ™ f)
    âˆ

  assoc-wrapâˆ˜delayâˆ™â†’Thunkable : forall {A : Ob p} {P : Ob +} {f : A â‡’ P}
    -> (wrap âˆ˜ delay) âˆ™ f â‰¡ wrap âˆ˜ (delay âˆ™ f)
    -> Thunkable f
  assoc-wrapâˆ˜delayâˆ™â†’Thunkable e {r = +} = sym assocâˆ™âˆ™
  assoc-wrapâˆ˜delayâˆ™â†’Thunkable {f = f} e {r = âŠ} {g = g} {h = h} =
    begin
      h âˆ˜ (g âˆ™ f)
    â‰¡Ë˜âŸ¨ cong (h âˆ˜_) (cong (_âˆ™ f) âˆ™forceâˆ˜delay) âŸ©
      h âˆ˜ (((g âˆ™ force) âˆ˜ delay) âˆ™ f)
    â‰¡âŸ¨ cong (h âˆ˜_) (helper1 e) âŸ©
      h âˆ˜ ((g âˆ™ force) âˆ˜ (delay âˆ™ f))
    â‰¡Ë˜âŸ¨ assocâˆ˜âˆ˜ âŸ©
      (h âˆ˜ (g âˆ™ force)) âˆ˜ (delay âˆ™ f)
    â‰¡Ë˜âŸ¨ helper1 e âŸ©
      ((h âˆ˜ (g âˆ™ force)) âˆ˜ delay) âˆ™ f
    â‰¡âŸ¨ cong (_âˆ™ f) assocâˆ˜âˆ˜ âŸ©
      (h âˆ˜ ((g âˆ™ force) âˆ˜ delay)) âˆ™ f
    â‰¡âŸ¨ cong (_âˆ™ f) (cong (h âˆ˜_) âˆ™forceâˆ˜delay) âŸ©
      (h âˆ˜ g) âˆ™ f
    âˆ

  helper2 : forall {N : Ob âŠ} {A : Ob p} {f : N â‡’ A}
    -> f âˆ˜ (unwrap âˆ™ force) â‰¡ (f âˆ˜ unwrap) âˆ™ force
    -> forall {B : Ob q} {h : B â‡’ â‡“ N} -> f âˆ˜ (unwrap âˆ™ h) â‰¡ (f âˆ˜ unwrap) âˆ™ h
  helper2 {f = f} e {h = h} =
    begin
      f âˆ˜ (unwrap âˆ™ h)
    â‰¡Ë˜âŸ¨ cong (f âˆ˜_) (cong (unwrap âˆ™_) forceâˆ˜delayâˆ™) âŸ©
      f âˆ˜ (unwrap âˆ™ (force âˆ˜ (delay âˆ™ h)))
    â‰¡Ë˜âŸ¨ cong (f âˆ˜_) assocâˆ™âˆ˜ âŸ©
      f âˆ˜ ((unwrap âˆ™ force) âˆ˜ (delay âˆ™ h))
    â‰¡Ë˜âŸ¨ assocâˆ˜âˆ˜ âŸ©
      (f âˆ˜ (unwrap âˆ™ force)) âˆ˜ (delay âˆ™ h)
    â‰¡âŸ¨ cong (_âˆ˜ (delay âˆ™ h)) e âŸ©
      ((f âˆ˜ unwrap) âˆ™ force) âˆ˜ (delay âˆ™ h)
    â‰¡âŸ¨ assocâˆ™âˆ˜ âŸ©
      (f âˆ˜ unwrap) âˆ™ (force âˆ˜ (delay âˆ™ h))
    â‰¡âŸ¨ cong ((f âˆ˜ unwrap) âˆ™_) forceâˆ˜delayâˆ™ âŸ©
      (f âˆ˜ unwrap) âˆ™ h
    âˆ

  assoc-âˆ˜unwrapâˆ™forceâ†’Linear : forall {N : Ob âŠ} {A : Ob p} {f : N â‡’ A}
    -> f âˆ˜ (unwrap âˆ™ force) â‰¡ (f âˆ˜ unwrap) âˆ™ force
    -> Linear f
  assoc-âˆ˜unwrapâˆ™forceâ†’Linear {f = f} e {q = +} {g = g} {h = h} =
    begin
      f âˆ˜ (g âˆ™ h)
    â‰¡Ë˜âŸ¨ cong (f âˆ˜_) (cong (_âˆ™ h) unwrapâˆ™wrapâˆ˜) âŸ©
      f âˆ˜ ((unwrap âˆ™ (wrap âˆ˜ g)) âˆ™ h)
    â‰¡âŸ¨ cong (f âˆ˜_) assocâˆ™âˆ™ âŸ©
      f âˆ˜ (unwrap âˆ™ ((wrap âˆ˜ g) âˆ™ h))
    â‰¡âŸ¨ helper2 e âŸ©
      (f âˆ˜ unwrap) âˆ™ ((wrap âˆ˜ g) âˆ™ h)
    â‰¡Ë˜âŸ¨ assocâˆ™âˆ™ âŸ©
      ((f âˆ˜ unwrap) âˆ™ (wrap âˆ˜ g)) âˆ™ h
    â‰¡Ë˜âŸ¨ cong (_âˆ™ h) assocâˆ™âˆ˜ âŸ©
      (((f âˆ˜ unwrap) âˆ™ wrap) âˆ˜ g) âˆ™ h
    â‰¡âŸ¨ cong (_âˆ™ h) (cong (_âˆ˜ g) âˆ˜unwrapâˆ™wrap) âŸ©
      (f âˆ˜ g) âˆ™ h
    âˆ
  assoc-âˆ˜unwrapâˆ™forceâ†’Linear e {q = âŠ} = sym assocâˆ˜âˆ˜
